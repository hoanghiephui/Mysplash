apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'org.greenrobot.greendao'


static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "com.wangdaye.mysplash"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 323
        versionName "3.2.3"
    }
    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java','.apt_generated']
            aidl.srcDirs = ['src/main/aidl','.apt_generated']
            assets.srcDirs = ['src/main/assets']
            res.srcDirs = [
                    'src/main/res/main',
                    'src/main/res/photo',
                    'src/main/res/user',
                    'src/main/res/collection',
                    'src/main/res/me',
                    'src/main/res/commen',
                    'src/main/res/about',
                    'src/main/res/search',
                    'src/main/res/resource',
                    'src/main/res']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = outputFile.name.replace("app",
                                "${defaultConfig.applicationId}_${defaultConfig.versionName}_${releaseTime()}")
                        output.outputFileName = fileName
                    }
                }
            }
        }
        buildTypes.each {
            it.buildConfigField 'String', 'APP_ID_BETA', APP_ID_BETA
            it.buildConfigField 'String', 'SECRET_BETA', SECRET_BETA
            it.buildConfigField 'String', 'APP_ID_RELEASE', APP_ID_RELEASE
            it.buildConfigField 'String', 'SECRET_RELEASE', SECRET_RELEASE

            it.buildConfigField 'String', 'FEED_TOKEN', FEED_TOKEN

            it.buildConfigField 'String', 'APP_ID_RELEASE_UNAUTH', APP_ID_RELEASE_UNAUTH
            it.buildConfigField 'String', 'GET_STREAM_KEY', GET_STREAM_KEY
            it.buildConfigField 'String', 'GET_STREAM_AUTH_CODE', GET_STREAM_AUTH_CODE
        }
    }
    repositories {
        mavenCentral()
    }
    signingConfigs {
        release {
            v2SigningEnabled false
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
}

greendao {
    schemaVersion 18
    generateTests false
}

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://jitpack.io"
        }
        maven {
            url "https://maven.google.com"
        }
    }
}

kapt {
    generateStubs = true
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'

    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:customtabs:26.1.0'

    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'

    compile 'com.jakewharton:butterknife:8.8.0'
    kapt 'com.jakewharton:butterknife-compiler:8.8.0'

    compile 'com.github.rahatarmanahmed:circularprogressview:2.5.0'
    compile 'com.pacioianu.david:ink-page-indicator:1.2.0'
    compile 'org.greenrobot:greendao:3.2.2'
    compile 'com.google.android.apps.muzei:muzei-api:2.0'
    implementation('com.github.ihsanbal:LoggingInterceptor:2.0.3') {
        exclude group: 'org.json', module: 'json'
    }
    implementation "pl.droidsonroids.gif:android-gif-drawable:1.2.8"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}
repositories {
    mavenCentral()
}